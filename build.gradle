buildscript {
  ext {
    groovyVersion = '3.0.9'
    spockVersion = '2.0-groovy-3.0'
    modelMapperVersion = "3.0.0"
    wiremockVersion = '2.32.0'
    springDocVersion = '1.6.6'
  }
}
plugins {
  id 'org.springframework.boot' version '2.6.3'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'groovy'
  id 'idea'
  id 'org.unbroken-dome.test-sets' version '4.0.0'
  id "io.freefair.lombok" version "6.4.0"
}

version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
  mavenCentral()
}

allprojects {
  group projectGroup

  apply plugin: 'java'
  apply plugin: 'java-library'
  apply plugin: 'groovy'
  apply plugin: 'org.unbroken-dome.test-sets'

  apply from: "${rootDir}/gradle/test.gradle"

  dependencies {
    annotationProcessor(group: 'org.projectlombok', name: 'lombok')
    compileOnly(group: 'org.projectlombok', name: 'lombok')
  }
}

subprojects {
  apply plugin: 'io.freefair.lombok'
  apply plugin: 'io.spring.dependency-management'
  apply plugin: 'org.springframework.boot'
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation "org.springdoc:springdoc-openapi-ui:$springDocVersion"
  runtimeOnly 'com.h2database:h2'
  implementation (project(':account-store-data'))
  implementation (project(':account-store-exchange-rates-fetcher'))
  testImplementation (project(':test-support'))
}

project(':common-mapping') {
  dependencies {
    api 'org.apache.commons:commons-lang3:3.12.0'
    api(project(':common-components'))
  }
}

project(':common-components') {
  dependencies {
    api 'org.springframework:spring-context'
    api "org.modelmapper:modelmapper:$modelMapperVersion"
    api "org.modelmapper.extensions:modelmapper-spring:$modelMapperVersion"
  }
}

project(':account-store-data') {
  dependencies {
    api 'org.springframework.boot:spring-boot-starter-data-jpa'
    api 'org.springframework:spring-context'
    api (project(':common-mapping'))
    api (project(':account-model'))
  }
}

project(':account-model') {}

project(':test-support') {
  dependencies {
    api 'org.springframework.boot:spring-boot-starter-test'
    api "org.spockframework:spock-core:$spockVersion"
    api "org.spockframework:spock-spring:$spockVersion"
    api "org.codehaus.groovy:groovy-all:$groovyVersion"
    api "com.github.tomakehurst:wiremock-jre8:$wiremockVersion"
  }
}

project(':account-store-exchange-rates-fetcher') {
  dependencies {
    api 'org.springframework.boot:spring-boot-starter-web'
    api 'org.springframework.boot:spring-boot-starter-actuator'
    api 'org.springframework.boot:spring-boot-starter-cache'
    testImplementation (project(':test-support'))
  }
}